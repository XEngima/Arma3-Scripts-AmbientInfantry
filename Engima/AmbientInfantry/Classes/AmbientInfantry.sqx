/*
 * Name:	AmbientInfantrytInstance
 * Date:	2017-07-17
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Class that handles the logic for all ambient infantry groups. Spawning and removals etc.
 */

call compile preprocessFileLineNumbers "Engima\AmbientInfantry\Classes\Configuration.sqx";
call compile preprocessFileLineNumbers "Engima\AmbientInfantry\Classes\InfantryGroup.sqx";

using Sqx.Markers;
using Sqx.Waypoints;

namespace Engima.AmbientInfantry.Classes
{
	public class AmbientInfantry
	{
		private fields ["_mConfiguration" as Configuration, "_mInfantryGroups" as Array, "_mMissionHasStarted" as Boolean];
	
		// Creates a Engine object.
		// _configuration (Configuration): The configuration for this instance of the
		public constructor {
			params ["_configuration" as Configuration];
			
			_mConfiguration = _configuration;
			_self.IsRunning = false;
			
			_mInfantryGroups = [];
			_mMissionHasStarted = false;
		};
		
		// Creates an instance of ambient infantry.
		// _parameters (Array): A key-value-paired list of configuration options.
		public static method AmbientInfantry CreateInstance {
			params ["_parameters" as Array];			
			private ["_configuration" as Configuration];
			
			_configuration = new Configuration;
			
			// Set all configuration values.
			private _side = [_parameters, "SIDE", _configuration.Side] call Configuration.GetParamValue;
			private _unitClasses = [_parameters, "UNIT_CLASSES", _configuration.UnitClasses] call Configuration.GetParamValue;
			private _maxGroupsCount = [_parameters, "MAX_GROUPS_COUNT", _configuration.MaxGroupsCount] call Configuration.GetParamValue;
			private _minUnitsInEachGroup = [_parameters, "MIN_UNITS_IN_EACH_GROUP", _configuration.MinUnitsInEachGroup] call Configuration.GetParamValue;
			private _maxUnitsInEachGroup = [_parameters, "MAX_UNITS_IN_EACH_GROUP", _configuration.MaxUnitsInEachGroup] call Configuration.GetParamValue;
			private _minSpawnDistance = [_parameters, "MIN_SPAWN_DISTANCE", _configuration.MinSpawnDistance] call Configuration.GetParamValue;
			private _maxSpawnDistance = [_parameters, "MAX_SPAWN_DISTANCE", _configuration.MaxSpawnDistance] call Configuration.GetParamValue;
			private _minSpawnDistanceOnStart = [_parameters, "MIN_SPAWN_DISTANCE_ON_START", _configuration.MinSpawnDistanceOnStart] call Configuration.GetParamValue;
			private _minSkill = [_parameters, "MIN_SKILL", _configuration.MinSkill] call Configuration.GetParamValue;
			private _maxSkill = [_parameters, "MAX_SKILL", _configuration.MaxSkill] call Configuration.GetParamValue;
			private _blacklistMarkers = [_parameters, "BLACKLIST_MARKERS", _configuration.BlacklistMarkers] call Configuration.GetParamValue;
			private _onGroupCreating = [_parameters, "ON_GROUP_CREATING", _configuration.OnGroupCreating] call Configuration.GetParamValue;
			private _onGroupCreated = [_parameters, "ON_GROUP_CREATED", _configuration.OnGroupCreated] call Configuration.GetParamValue;
			private _onGroupRemoving = [_parameters, "ON_GROUP_REMOVING", _configuration.OnGroupRemoving] call Configuration.GetParamValue;
			private _onCheckReleaseGroup = [_parameters, "ON_CHECK_RELEASE_GROUP", _configuration.OnCheckReleaseGroup] call Configuration.GetParamValue;
			private _onGroupReleased = [_parameters, "ON_GROUP_RELEASED", _configuration.OnGroupReleased] call Configuration.GetParamValue;
			private _inDebugMode = [_parameters, "IN_DEBUG_MODE", _configuration.InDebugMode] call Configuration.GetParamValue;

			_configuration.Side = _side;
			_configuration.UnitClasses = _unitClasses;
			_configuration.MaxGroupsCount = _maxGroupsCount;
			_configuration.MinUnitsInEachGroup = _minUnitsInEachGroup;
			_configuration.MaxUnitsInEachGroup = _maxUnitsInEachGroup;
			_configuration.MinSpawnDistance = _minSpawnDistance;
			_configuration.MaxSpawnDistance = _maxSpawnDistance;
			_configuration.MinSpawnDistanceOnStart = _minSpawnDistanceOnStart;
			_configuration.MinSkill = _minSkill;
			_configuration.MaxSkill = _maxSkill;
			_configuration.BlacklistMarkers = _blacklistMarkers;
			_configuration.OnGroupCreating = _onGroupCreating;
			_configuration.OnGroupCreated = _onGroupCreated;
			_configuration.OnGroupRemoving = _onGroupRemoving;
			_configuration.OnCheckReleaseGroup = _onCheckReleaseGroup;
			_configuration.OnGroupReleased = _onGroupReleased;
			_configuration.InDebugMode = _inDebugMode;
			
			private _instance = [_configuration] new AmbientInfantry;
			call _instance.Start;
			_instance
		};
		
		// Finds a suitable spawn position for an infantry group.
		// Returns (Array): A position. An empty array if no position could be found.
		private method Array FindSpawnPos {
			params ["_playerPositions" as Array];
			
			private ["_playerPos" as Array];
			private _spawnPos = [];
			private _tries = 0;

			private _minSpawnDistance = _mConfiguration.MinSpawnDistanceOnStart;
			if (_mMissionHasStarted) then {
				_minSpawnDistance = _mConfiguration.MinSpawnDistance
			};

			// Select the player surrounded by less groups.
			private _player1Pos = ((selectRandom _playerPositions) as PlayerPosition).AheadPosition;
			private _player2Pos = ((selectRandom _playerPositions) as PlayerPosition).AheadPosition;
			private _player1Count = 0;
			private _player2Count = 0;
			
			{
				private _pos = _x.Position;
				
				if (_pos distance2D _player1Pos < _pos distance2D _player2Pos) then {
					_player1Count = _player1Count + 1;
				}
				else {
					_player2Count = _player2count + 1;
				};
			} foreach _mInfantryGroups as InfantryGroup;
			
			if (_player1Count > _player2Count) then {
				_playerPos = _player2Pos;
			}
			else {
				_playerPos = _player1Pos;
			};
			
			while { count _spawnPos == 0 && count _playerPositions > 0 && _tries <= 20 } do
			{
				private _x = (_playerPos select 0) - _mConfiguration.MaxSpawnDistance + random (2 * _mConfiguration.MaxSpawnDistance);
				private _y = (_playerPos select 1) - _mConfiguration.MaxSpawnDistance + random (2 * _mConfiguration.MaxSpawnDistance);
				private _pos = [_x, _y, 0.2];
				private _insideBlacklistMarker = false;
				
				// Check that the position is not inside a blacklist marker
				{
					if (_pos inArea _x) then {
						_insideBlacklistMarker = true;
					};
				} foreach _mConfiguration.BlacklistMarkers as String;
				
				private _tooCloseToPlayer = false;
				private _tooFarFromPlayers = true;
				
				if (!surfaceIsWater _pos && !_insideBlacklistMarker) then {
					{
						scopeName "current";
					
						if (_x.AheadPosition distance _pos < _minSpawnDistance) then {
							_tooCloseToPlayer = true;
							breakOut "current";
						};
					} foreach _playerPositions as PlayerPosition;
					
					if (!_tooCloseToPlayer) then {
						{
							scopeName "current";
							
							if (_x.AheadPosition distance _pos < _mConfiguration.MaxSpawnDistance) then {
								_tooFarFromPlayers = false;
								breakOut "current";
							};
						} foreach _playerPositions as PlayerPosition;
						
						if (!_tooFarFromPlayers) then {
							_spawnPos = _pos;
						};
					};
					
					sleep 0.1;
				};
				
				_tries = _tries + 1;
			};
			
			_spawnPos
		};
		
		// Get whether this instance is running or not. true if it is running, otherwise false.
		public property Boolean IsRunning { get; private set; };
		
		// Calculates the blacklist coverage share for one player.
		// _playerPos (Array): The player's position.
		// Returns (Scalar): Coverage chare for surrounding blacklist markers. 0 if no area is
		//                   covered by blacklist marker, and 1 if all area is covered.
		private method Scalar CalculatePlayerBlacklistCoverage {
			params ["_playerPos" as PlayerPosition];
			
			private _minSpawnDistance = _mConfiguration.MinSpawnDistance;
			private _maxSpawnDistance = _mConfiguration.MaxSpawnDistance;
			private _playerRadius = _mConfiguration.MaxSpawnDistance;
			private _blacklistMarkers = _mConfiguration.BlacklistMarkers;
			private _sumCoveredShareBlacklist = 0;
			
			{
				private _distanceToMarker = _playerPos.AheadPosition distance2D (getMarkerPos _x);
				private _avgMarkerRadius = (((getMarkerSize _x) select 0) + ((getMarkerSize _x) select 1)) / 2;
				private _coveredShare = 0;
				
				if (_distanceToMarker < _playerRadius + _avgMarkerRadius) then {
					// Om blacklistmarkern inte Ã¤r helt omsluten av playerradius
					if (_playerRadius > _distanceToMarker + _avgMarkerRadius) then {
						_coveredShare = _avgMarkerRadius / _playerRadius;
					}
					else {
						_coveredShare = (_playerRadius + _avgMarkerRadius - _distanceToMarker) / (2 * _playerRadius);
					};
				};

				_sumCoveredShareBlacklist = _sumCoveredShareBlacklist + _coveredShare;
			} foreach _blacklistMarkers as String;
			
			private _circleParts = 12;
			private _waterCount = 0;
			private _sumWaterCoveredShare = 0;
			
			for "_i" from 1 to _circleParts do {
				private _x = (_playerPos.AheadPosition select 0) + _minSpawnDistance * sin (_i * 360 / _circleParts);
				private _y = (_playerPos.AheadPosition select 1) + _minSpawnDistance * cos (_i * 360 / _circleParts);
				private _closePos = [_x, _y];
				
				_x = (_playerPos.AheadPosition select 0) + _maxSpawnDistance * sin (_i * 360 / _circleParts);
				_y = (_playerPos.AheadPosition select 1) + _maxSpawnDistance * cos (_i * 360 / _circleParts);
				private _farPos = [_x, _y];
				
				if (surfaceIsWater _closePos && !([_closePos, _mConfiguration.BlacklistMarkers] call MarkerHelper.PositionInsideAnyMarker)) then {
					_waterCount = _waterCount + 1;
				};
				
				if (surfaceIsWater _farPos && !([_farPos, _mConfiguration.BlacklistMarkers] call MarkerHelper.PositionInsideAnyMarker)) then {
					_waterCount = _waterCount + 1;
				};
			};

			_sumWaterCoveredShare = _waterCount / (_circleParts * 2);
			
			_sumCoveredShareBlacklist + _sumWaterCoveredShare
		};
		
		// Calculates how much area that is covered when blacklist markers are taken account of,
		// and takes all players and all blacklist markers in account.
		// _playerPositions (Array): All players' positions.
		// Returns (Scalar): The average share of area covered by blacklist markers. 0 = Nothing 
		// covered, 1 = all covered.
		private method Scalar CalculateBlacklistCoverage {
			params ["_playerPositions" as Array];
			
			private _sumShare = 0;
			
			{
				_sumShare = _sumShare + ([_x] call _self.CalculatePlayerBlacklistCoverage);
			} foreach _playerPositions as PlayerPosition;
			
			_sumShare / count _playerPositions
		};
		
		// Runs the ambient infantry instance. Spawning and removing units accoring to the configuration.
		// The method should be spawned.
		private method Run {
			private ["_skill" as Scalar, "_group" as Group, "_unitsCount" as Scalar];
			private ["_calculatedGroupsCount" as Scalar];
			private _groupsCount = 0 as Scalar;
			private _spawnPos = [] as Array;
			private _iteration = 0;
			
			if (isNil "Sqx_Markers_Marker_UnitCurrentId") then {
				Sqx_Markers_Marker_UnitCurrentId = 1;
			};
			
			private _closeCircleMarker = "";
			private _farCircleMarker = "";
			
			if (_mConfiguration.InDebugMode && { local player }) then {
				_closeCircleMarker = createMarkerLocal ["ENG_CloseMarker", getPos vehicle player];
				_closeCircleMarker setMarkerShapeLocal "ELLIPSE";
				_closeCircleMarker setMarkerSizeLocal [_mConfiguration.MinSpawnDistance, _mConfiguration.MinSpawnDistance];
				_closeCircleMarker setMarkerColorLocal "ColorRed";
				_closeCircleMarker setMarkerBrushLocal "Border";
				
				_farCircleMarker = createMarkerLocal ["ENG_FarMarker", getPos vehicle player];
				_farCircleMarker setMarkerShapeLocal "ELLIPSE";
				_farCircleMarker setMarkerSizeLocal [_mConfiguration.MaxSpawnDistance, _mConfiguration.MaxSpawnDistance];
				_farCircleMarker setMarkerColorLocal "ColorBlue";
				_farCircleMarker setMarkerBrushLocal "Border";
			};
			while { true } do
			{
				private _playerPositions = call Helper.GetAllPlayerPositions;
				
				if (_mConfiguration.InDebugMode && { local player }) then {
					{
						_closeCircleMarker setMarkerPosLocal _x.Position;
						_farCircleMarker setMarkerPosLocal _x.AheadPosition;
					} foreach _playerPositions as PlayerPosition;
				};
				
				if (count _playerPositions > 0) then {
					private _coveredShare = [_playerPositions] call _self.CalculateBlacklistCoverage;
					
					#region Removing Groups
					
					// Make a list of all units to keep (that are not too far away from any player)
					private _keptGroups = [] as Array;
					
					// for each infantry group in _infantryGroups:
					{
						private _infantryGroup = _x;
						private _releaseGroup = [_infantryGroup.Group, _groupsCount] call _mConfiguration.OnCheckReleaseGroup;
	
						if (!_releaseGroup is Boolean) then {
							_releaseGroup = false;
						};
						
						private _groupPos = call _x.UpdatePosition;
						private _closestPlayerDistance = 1000000;
						private _keepGroup = false;
						{
							scopeName "innerLoop";
							
							private _distance = (_x.AheadPosition distance2D _groupPos);
							if (_distance < _closestPlayerDistance) then {
								_closestPlayerDistance = _distance;
								
								if (_closestPlayerDistance < _mConfiguration.MaxSpawnDistance || { (_x.Position) distance2D _groupPos < (_x.Position) distance2D (_x.AheadPosition) }) then {
									_keepGroup = true;
									breakOut "innerLoop";
								};
							};							
						} foreach (call Helper.GetAllPlayerPositions) as PlayerPosition;
	
						if (!_keepGroup || _releaseGroup) then {
							if (_releaseGroup) then {
								_groupsCount = _groupsCount - 1;
								[_infantryGroup.Group] call WaypointHelper.DeleteAllWaypointsFromGroup;
								[_infantryGroup.Group, _groupsCount] call _mConfiguration.OnGroupReleased;
							}
							else {
								[_infantryGroup.Group, _groupsCount] call _mConfiguration.OnGroupRemoving;
						
								call _infantryGroup.Dispose;
								
								_groupsCount = _groupsCount - 1;
								
								if (_mConfiguration.InDebugMode) then {
									player commandChat "Removing ambient infantry group. (Total: " + str _groupsCount + ")";;
								};
							};
						}
						else {
							_keptGroups pushBack _infantryGroup;
							call _infantryGroup.CheckEnteredRestrictedArea;
						};
					} foreach _mInfantryGroups as InfantryGroup;
					
					_mInfantryGroups = _keptGroups;
					_groupsCount = count _keptGroups;
					
					#endregion
					
					#region Creating Gruops
					
					_calculatedGroupsCount = round (_mConfiguration.MaxGroupsCount * (1 - _coveredShare));
				
					if (_groupsCount < _calculatedGroupsCount) then
					{
						// Find a spawn position
						_spawnPos = [_playerPositions] call _self.FindSpawnPos;
					
						// If a spawn position could be found
						if (count _spawnPos > 0) then {
							_unitsCount = ceil (_mConfiguration.MinUnitsInEachGroup + random (_mConfiguration.MaxUnitsInEachGroup - _mConfiguration.MinUnitsInEachGroup));
							private _unitClasses = [] as Array;
							
							for "_i" from 1 to _unitsCount do {
								_unitClasses pushBack (selectRandom _mConfiguration.UnitClasses);
							};
							
							private _paramsArray = [_spawnPos, _unitClasses];							
							private _goOnWithSpawn = [_paramsArray, _groupsCount, _calculatedGroupsCount] call _mConfiguration.OnGroupCreating;

                            // Retrieve the possibly altered values
                            _spawnPos = _paramsArray select 0;
                            _unitClasses = _paramsArray select 1;
                            
                            // If the user has not messed something up, use the edited class list
                            private _userMessedUp = false;
                            private _logMsg = "";
                            if (count _paramsArray != 2) then {
                                _userMessedUp = true;
                                _logMsg = "Engima.AmbientInfantry: Error - Altered params array in OnGroupCreating has wrong number of items. Should be 2.";
                            };
                            if (isNil "_spawnPos" || { !(_spawnPos isEqualTypeArray [0,0] || _spawnPos isEqualTypeArray [0,0,0]) }) then {
                                _spawnPos = [0,0,0];
                                _userMessedUp = true;
                                _logMsg = "Engima.AmbientInfantry: Error - Altered parameter 0 in OnGroupCreating is not a position. Must be on format [0,0,0]";
                            };
                            if (isNil "_unitClasses" || { !_unitClasses is Array } || count _unitClasses == 0) then {
                                _unitClasses = [];
                                _userMessedUp = true;
                                _logMsg = "Engima.AmbientInfantry: Error - Altered parameter 1 in OnGroupCreating is not an array. Must be an array with unit class names.";
                            };
                            
                            if (isNil "_goOnWithSpawn") then {
                                _goOnWithSpawn = false;
                                _userMessedUp = true;
                                _logMsg = "Engima.AmbientInfantry: Error - Return value of callback OnGroupCreating is nil. Must be true or false.";
                            };
                            
                            if (_userMessedUp) then {
                                diag_log _logMsg;
                                player sideChat _logMsg;
                            };
                            
							if (_goOnWithSpawn && {count _unitClasses > 0} && {!_userMessedUp}) then {
								_group = createGroup _mConfiguration.Side;
								
								{
									_skill = _mConfiguration.MinSkill + random (_mConfiguration.MaxSkill - _mConfiguration.MinSkill);
									_x createUnit [_spawnPos, _group, "", _skill];
								} foreach _unitClasses as String;
								
								{
									private _varName = "Sqx_Markers_Marker_Unit_" + str Sqx_Markers_Marker_UnitCurrentId;
									Sqx_Markers_Marker_UnitCurrentId = Sqx_Markers_Marker_UnitCurrentId + 1;
									_x setVehicleVarName _varName;
									_x call compile format ["%1 = _this; publicVariable ""%1"";", _varName];
								} foreach (units _group) as Object;
								
								_mInfantryGroups pushBack [_group, _mConfiguration] new InfantryGroup;
								_groupsCount = _groupsCount + 1;
								
								[_group, _groupsCount] call _mConfiguration.OnGroupCreated;
								
								if (_mConfiguration.InDebugMode) then {
									player commandChat "Creating ambient infantry group. (Total: " + str _groupsCount + ")";
								};
							};
						};
					};
					
					#endregion
				};
			
				_iteration = _iteration + 1;
				hint str _iteration;
				
				// Sleep a while
				if (_iteration > _mConfiguration.MaxGroupsCount) then {
					_mMissionHasStarted = true;
					sleep 3;
				};
			};
		};
		
		// Starts the ambient infantry instance if it has not already started.
		public method Start {
			if (!_self.IsRunning) then {
				_self.IsRunning = true;
				[] spawn _self.Run;
			};
		};
	};
};
